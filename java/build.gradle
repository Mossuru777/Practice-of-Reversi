allprojects {
    version '1.0-SNAPSHOT'

    repositories {
        jcenter()
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    dependencies {
        compile 'org.jetbrains:annotations:17.0.0'
        compile 'org.json:json:20180813'
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main']
            }
        }

        test {
            java {
                srcDirs = ['src/test']
            }
        }
    }
}

configure([project(':server'), project(':client')]) {
    apply plugin: 'application'
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compile project(':common')
        compile 'args4j:args4j:2.33'
    }

    jar {
        archiveFileName = "${archiveBaseName}.${archiveExtension}"
    }

    shadowJar {
        classifier = null
        version = null
    }
}
